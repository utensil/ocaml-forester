(*
 * SPDX-FileCopyrightText: 2024 The Forester Project Contributors
 *
 * SPDX-License-Identifier: GPL-3.0-or-later
 *)

open Forester_prelude
open Forester_core
open Forester_frontend
open Forester_compiler
open Cmdliner
module EP = Eio.Path

let setup_logs style_renderer level =
  Fmt_tty.setup_std_outputs ?style_renderer ();
  Logs.set_level level;
  Logs.set_reporter (Logs_fmt.reporter ());
  ()

let verbosity =
  let env = Cmd.Env.info "PART_LOGS" in
  Logs_cli.level ~env ~docs:Manpage.s_common_options ()

let renderer =
  let env = Cmd.Env.info "PART_FMT" in
  Fmt_cli.style_renderer ~docs:Manpage.s_common_options ~env ()

let arg_logs = Term.(const setup_logs $ renderer $ verbosity)

let version =
  let major =
    match Build_info.V1.version () with
    | None -> "n/a"
    | Some v -> Build_info.V1.Version.to_string v
  in
  Format.asprintf "%s" major

let build ~env _ config_filename dev no_theme =
  Reporter.easy_run @@ fun () ->
  let config = Config_parser.parse_forest_config_file config_filename in
  Logs.debug (fun m -> m "Parsed config file %s" config_filename);
  let forest = Driver.batch_run ~env ~dev ~config in
  forest.diagnostics
  |> URI.Tbl.iter (fun _ d -> List.iter Reporter.Tty.display d);
  if not no_theme then begin
    Logs.warn (fun m ->
        m
          "You are using a development build of forester. Ignoring local theme \
           and using canonical theme instead.");
    let@ () = Reporter.trace "when copying theme directory" in
    let theme_dir = List.hd Theme_site.Sites.theme ^ "/theme" in
    Format.printf "%s@." theme_dir;
    Forester.copy_contents_of_dir ~env ~forest
    @@ Eio_util.path_of_dir ~env theme_dir
  end;
  Forester.render_forest ~dev ~forest;
  Logs.app (fun m -> m "Success!")

let new_tree ~env config_filename dest_dir prefix template random =
  let@ () = Reporter.silence in
  let config = Config_parser.parse_forest_config_file config_filename in
  let forest = Driver.batch_run ~env ~dev:true ~config in
  let mode = if random then `Random else `Sequential in
  let new_tree =
    Forester.create_tree ~env ~dest_dir ~prefix ~template ~mode ~forest
  in
  Format.printf "%s" new_tree

let complete ~env config_filename title =
  let@ () = Reporter.silence in
  let config = Config_parser.parse_forest_config_file config_filename in
  let forest = Driver.batch_run ~env ~dev:true ~config in
  let@ uri, title = List.iter @~ Forester.complete ~forest title in
  Format.printf "%s, %s\n" uri title

let query_all ~env config_filename =
  let@ () = Reporter.silence in
  let config = Config_parser.parse_forest_config_file config_filename in
  let forest = Driver.batch_run ~env ~config ~dev:true in
  Format.printf "%s" (Forester.json_manifest ~dev:true ~forest)

let default_config_str =
  {|[forest]
trees = ["trees" ]  # The directories in which your trees are stored
assets = ["assets"] # The directories in which your assets are stored
url = "https://www.my-great-forest.net/" # Replace this with your own domain or web storage. If you don't have one, you can use "http://localhost/"; the URL given here does not matter unless you plan to publish your forest.
|}

let index_tree_str =
  {|\title{Hello, World!}
\p{Welcome to your first tree! This tree is the root of your forest.}
\ul{
  \li{[Build and view your forest for the first time](https://www.forester-notes.org/jms-007D/)}
  \li{[Overview of the Forester markup language](https://www.forester-notes.org/jms-007N/)}
  \li{[Creating new trees](https://www.forester-notes.org/jms-007H/)}
  \li{[Creating your personal biographical tree](https://www.forester-notes.org/jms-007K/)}
}
|}

let init ~env dir =
  let default_theme_url =
    "https://git.sr.ht/~jonsterling/forester-base-theme"
  in
  let theme_version = "5.0" in
  let cwd =
    match dir with
    | None -> Eio.Stdenv.cwd env
    | Some d ->
      begin try EP.mkdir ~perm:0o755 EP.(Eio.Stdenv.cwd env / d)
      with _ ->
        Reporter.emit Initialization_warning
          ~extra_remarks:
            [Asai.Diagnostic.loctextf "Directory `%s` already exists" d]
      end;
      EP.(Eio.Stdenv.cwd env / d)
  in
  begin try
    let proc_mgr = Eio.Stdenv.process_mgr env in
    let@ cmd =
      List.iter
      @~ [
           ["git"; "init"; "--quiet"];
           ["git"; "branch"; "-m"; "main"];
           ["git"; "submodule"; "add"; default_theme_url; "theme"];
           ["git"; "-C"; "theme"; "checkout"; theme_version];
         ]
    in
    Eio.Process.run ~cwd proc_mgr cmd
  with exn ->
    Reporter.fatal Configuration_error
      ~extra_remarks:
        [
          Asai.Diagnostic.loctextf
            {|
Failed to set up theme: %a. To perform this step manually, run the commands

git init
git submodule add %s
git -C theme checkout %s
        |}
            Eio.Exn.pp exn default_theme_url theme_version;
        ]
  end;
  ["trees"; "assets"] |> List.iter (Eio_util.try_create_dir ~cwd);
  Eio_util.try_create_file ~cwd ~content:default_config_str "forest.toml";
  Eio_util.try_create_file ~cwd ~content:"output/" ".gitignore";
  Eio_util.try_create_file ~cwd ~content:"" "assets/.gitkeep";
  Eio_util.try_create_file ~cwd ~content:index_tree_str "trees/index.tree";
  Reporter.emit Log
    ~extra_remarks:
      [
        Asai.Diagnostic.loctextf "%s"
          "Initialized forest, try editing `trees/index.tree` and running \
           `forester build`. Afterwards, you can open `output/index.html` in \
           your browser to view your forest.";
      ]

let arg_config =
  let doc = "A TOML file like $(i,forest.toml)" in
  Arg.(value & pos 0 file "forest.toml" & info [] ~docv:"FOREST" ~doc)

let build_cmd ~env:env_ =
  let arg_dev =
    let doc =
      "Run forester in development mode; this will attach source file \
       locations to the generated json."
    in
    Arg.value @@ Arg.flag @@ Arg.info ["dev"] ~doc
  in
  let arg_no_theme =
    let doc = "Build without copying the theme directory" in
    Arg.value @@ Arg.flag @@ Arg.info ["no-theme"] ~doc
  in
  let doc = "Build the forest" in
  let man =
    [
      `S Manpage.s_description;
      `P
        "The $(tname) command builds a hypertext $(b,forest) from trees stored \
         in each $(i,INPUT_DIR) or any of its subdirectories; tree files are \
         expected to be of the form $(i,addr.tree) where $(i,addr) is the \
         address of the tree. Note that the physical location of a tree is not \
         taken into account, and two trees with the same address are not \
         permitted.";
    ]
  in
  let info = Cmd.info "build" ~version ~doc ~man in
  Cmd.v info
    Term.(
      const (build ~env:env_) $ arg_logs $ arg_config $ arg_dev $ arg_no_theme)

let new_tree_cmd ~env:env_ =
  let arg_prefix =
    let doc = "The namespace prefix for the created tree." in
    Arg.value
    @@ Arg.opt (Arg.some Arg.string) None
    @@ Arg.info ["prefix"] ~docv:"XXX" ~doc
  in
  let arg_template =
    let doc = "The tree to use as a template" in
    Arg.value
    @@ Arg.opt (Arg.some Arg.string) None
    @@ Arg.info ["template"] ~docv:"XXX" ~doc
  in
  let arg_dest_dir : string option Term.t =
    let doc = "The directory in which to deposit created tree." in
    Arg.value
    @@ Arg.opt (Arg.some Arg.dir) None
    @@ Arg.info ["dest"] ~docv:"DEST" ~doc
  in
  let arg_random =
    let doc =
      "True if the new tree should have id assigned randomly rather than \
       sequentially"
    in
    Arg.value @@ Arg.flag @@ Arg.info ["random"] ~doc
  in
  let doc = "Create a new tree." in
  let info = Cmd.info "new" ~version ~doc in
  Cmd.v info
    Term.(
      const (new_tree ~env:env_)
      $ arg_config $ arg_dest_dir $ arg_prefix $ arg_template $ arg_random)

let complete_cmd ~env:env_ =
  let arg_title =
    let doc = "The tree title prefix to complete." in
    Arg.value @@ Arg.opt Arg.string "" @@ Arg.info ["title"] ~docv:"title" ~doc
  in
  let doc = "Complete a tree title." in
  let info = Cmd.info "complete" ~version ~doc in
  Cmd.v info Term.(const (complete ~env:env_) $ arg_config $ arg_title)

let query_all_cmd ~env:env_ =
  let doc = "List all trees in JSON format" in
  let info = Cmd.info "all" ~version ~doc in
  Cmd.v info Term.(const (query_all ~env:env_) $ arg_config)

let query_cmd ~env =
  let doc = "Query your forest" in
  let info = Cmd.info "query" ~version ~doc in
  Cmd.group info [query_all_cmd ~env]

let init_cmd ~env:env_ =
  let arg_dir =
    let doc = "The directory in which to initialize the forest" in
    Arg.value
    @@ Arg.opt (Arg.some Arg.string) None
    @@ Arg.info ["dir"] ~docv:"DIR" ~doc
  in
  let doc = "Initialize a new forest" in
  let man =
    [
      `S Manpage.s_description;
      `P
        "The $(tname) command initializes a $(b,forest) in the current \
         directory. This involves initialising a git repository, setting up a \
         git submodule for the theme, creating an assets and trees directory, \
         as well as a config file.";
    ]
  in
  let info = Cmd.info "init" ~version ~doc ~man in
  Cmd.v info Term.(const (init ~env:env_) $ arg_dir)

let lsp ~env _ config =
  let config = Config_parser.parse_forest_config_file config in
  Forester_lsp.start ~env ~config

let lsp_cmd ~env:env_ =
  let man =
    [
      `S Manpage.s_description;
      `P "The $(tname) command starts the forester language server.";
    ]
  in
  let doc = "Start the LSP" in
  let info = Cmd.info "lsp" ~version ~doc ~man in
  Cmd.v info Term.(const (lsp ~env:env_) $ arg_logs $ arg_config)

let cmd ~env =
  let doc = "a tool for tending mathematical forests" in
  let man =
    [
      `S Manpage.s_bugs;
      `P "Email bug reports to <~jonsterling/forester-discuss@lists.sr.ht>.";
      `S Manpage.s_authors;
      `P "Jonathan Sterling";
    ]
  in
  let info = Cmd.info "forester" ~version ~doc ~man in
  Cmd.group info
    [
      build_cmd ~env;
      new_tree_cmd ~env;
      complete_cmd ~env;
      init_cmd ~env;
      query_cmd ~env;
      lsp_cmd ~env;
    ]

let () =
  Random.self_init ();
  Printexc.record_backtrace true;
  Logs.set_reporter (Logs_fmt.reporter ());
  let@ env = Eio_main.run in
  let@ () = Forester_core.Reporter.easy_run in
  exit @@ Cmd.eval ~catch:false @@ cmd ~env
